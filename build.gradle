buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "maven-publish"
    id "java"
}

group 'com.bgpixel'
version '1.1.0'

sourceCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "knezmilos13"
            name "Milos Milutinovic"
            email "knezmilos13@gmail.com"
        }
    }

    scm {
        url "https://github.com/knezmilos13/prettyboxformatter"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.bgpixel'
            artifactId 'prettyboxformatter'
            version '1.2.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library for formatting/serializing text into boxes drawn using UTF-8 characters.')
                root.appendNode('name', 'Pretty Box Formatter')
                root.appendNode('url', 'https://github.com/knezmilos13/prettyboxformatter')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'PrettyBoxFormatter'
        name = 'PrettyBoxFormatter'
        userOrg = 'knezmilos13'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/knezmilos13/prettyboxformatter'
        version {
            name = '1.2.0'
            desc = '1.2.0'
            released  = new Date()
        }
    }

    publish = true
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
}
